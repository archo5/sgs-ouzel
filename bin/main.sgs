
ouzel.setAppAndDeveloperNames( "sgs-ouzel Samples", "org.sgscript" );
ouzel.setScreenSaverEnabled( false );
ouzel.window.title = "sgs-ouzel Samples";
ouzel.fileSystem.addResourcePath( "Resources" );

ouzel.renderer.clearColor = ouzel.Color( 64, 0, 0 );

class Scene
{
	function onUpdate( dt ){}
	function onKeyboardEvent( type, e ){}
	function onMouseEvent( type, e ){}
	function onGamepadEvent( type, e ){}
	function onTouchEvent( type, e ){}
	function onUIEvent( type, e ){}
}

class Sample : Scene
{
	function onGamepadEvent( type, e )
	{
		if( type == ouzel.EventType.GAMEPAD_BUTTON_CHANGE )
		{
			if( e.pressed && e.button == ouzel.input.GamepadButton.FACE_RIGHT )
			{
				_G.curScene = new MainMenu();
			}
		}
		return true;
	}
	function onKeyboardEvent( type, e )
	{
		if( type == ouzel.EventType.KEY_PRESS )
		{
			if( e.key == ouzel.input.KeyboardKey.ESCAPE || e.key == ouzel.input.KeyboardKey.MENU )
			{
				_G.curScene = new MainMenu();
			}
		}
		return true;
	}
	function onUIEvent( type, e )
	{
		if( type == ouzel.EventType.ACTOR_CLICK )
		{
			if( e.actor === this.backButton )
			{
				_G.curScene = new MainMenu();
			}
		}
		return true;
	}
}

include
	"main-menu",
	"main-sample-sprites",
	"main-sample-gui",
	"main-sample-rt",
	"main-sample-animations",
	"main-sample-input",
	"main-sample-sound"
;

global curScene = new MainMenu();

global eventHandler = ouzel.createEventHandler();
function eventHandler.onGamepadEvent( type, e ){ curScene.onGamepadEvent( type, e ); }
function eventHandler.onUIEvent( type, e ){ curScene.onUIEvent( type, e ); }
function eventHandler.onKeyboardEvent( type, e ){ curScene.onKeyboardEvent( type, e ); }
function eventHandler.onMouseEvent( type, e ){ curScene.onMouseEvent( type, e ); }
function eventHandler.onTouchEvent( type, e ){ curScene.onTouchEvent( type, e ); }

global updateCallback = ouzel.createUpdateCallback();
function updateCallback.onUpdate( dt ){ curScene.onUpdate( dt ); }

